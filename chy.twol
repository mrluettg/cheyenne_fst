Alphabet 



a e h k l ʼ m n o p s š t v x ė ȯ ȧ á é ó b c d f g i j k l q r u w y z



%{n%}:n %{n%}:0 !odd possessive prefix

%{v%}:v %{v%}:0 !odd possessive prefix

%{é%}:é 	!odd possessive prefix

%{ǒ%}:o %{ǒ%}:0 !obviatiative nouns 

%{ě%}:e %{ě%}:0 !possessive suffux

%{e%}:0 %{e%}:e !ke, pe, ʼe, etc. 

%{o%}:0 %{o%}:o %{o%}:e !for plural oʼo

%{s%}:0 %{s%}:s !tse

%{ʼ%}:0 %{ʼ%}:ʼ !for plural oʼo 

%{Ó%}:0 !for variation of IA obviatative. 

%{E%}:e %{E%}:é !for variation of IA obviatative. 

%{ē%}:e		!an e that NEVER changes

%{ō%}:o		!an o that NEVER changes





%{N%}:0



%>:0 



; 



Sets 



Vow = a e o ȧ ė ȯ á é ó %{E%};



Con = h k l ʼ m n p s š t v x ;



HighVow = á é ó ; !this refers to tone. 



VoicelessFric = h s š x ; !deleted if no following vowel



DeleteCon = h s š n ;



AddECon = k l ʼ m p v ; !t technicially in here. but ends in e



FinalMultichar = %{e%} %{o%} %{s%} %{ʼ%} ;



Eng = a b c d e f g h i j k l m n o p q r s t u v w x y z ;



Chy = a e h k l ʼ m n o p s š t v x ė ȯ ȧ á é ó ;









Rules



"remove end consonant if no morpheme boundary"

[h:0 | s:0 | š:0 | n:0] <=> _ DeleteCon:* FinalMultichar:* .#. ; 

		except Vow: Vow: _ %{o%}: .#. ;

			_ Eng:* %{N%} ;



"accomplish t:tse <=> _ .#. t/%{s%}"

%{s%}:s <=> _ %{e%}: .#. ; 

except _ Eng:* %{N%} ; 



"Accomplish [ʼ:ʼe | m:me ] <=> _ .#. ;"

%{e%}:0 <=> _ %>: ; 



"Accomplish xaon -> xaoʼo/// n"

Cx:ʼ <=> Vow: Vow: _ %{o%}: .#. ;

	where Cx in (h s š n) ; 



"Accomplish xaon -> xaoʼo/// o"

%{o%}:Vx <=> Vow: Vy: DeleteCon: _ .#. ;

	where Vx in (e o)

		Vy in (e o)

	matched ;



"Accomplish xaon -> xaoʼo/// o but where o has an accent"

%{o%}:Vx <=> Vow: Vy: DeleteCon: _ .#. ;

	where Vx in (e o)

		Vy in (é ó)

	matched ;




!@ v:v é: h:h a:a n:n a:a m:m {e}:0



"Accomplish  nákȯhéo -> nákȯhéoʼo /// o"

%{o%}:o <=> Vow: %>: Vow: %{ʼ%}: _ ;

	%{ǒ%}: %>: Vow: %{ʼ%}: _ ;

"Accomplish  nákȯhéo -> nákȯhéoʼo /// ʼ"

%{ʼ%}:ʼ <=> Vow: %>: Vow: _ ;

	%{ǒ%}: %>: Vow: _ ;









"o and e become voiceless before t"

[a:ȧ | e:ė | o:ȯ ] <=> _ t: s:  ;

			_ t: %{s%}:  ; 

		except

			_ Eng:* %{N%} ;

			Vow: _ ;

			_ t: %{s%}: %{e%}: %>: [a: | o: | ȧ: | ȯ: | á: | ó: ] ; ! so no méʼėševȯto

			





"neutralization of é and ó at end of word"

[é:e | ó:o ] <=>  _ Con:* FinalMultichar:* .#. ;

			_ Vow: Con: FinalMultichar:* .#. ;!meeʼe

			_ (Con:) >: Vow: FinalMultichar:* ;!to prevent hexovóno ón>o{ʼ}{o}

			except Vow: _ FinalMultichar:* .#. ;

				Vow: _ Con: FinalMultichar:* .#. ;

"Neutralization for á"

[á:a] <=> _ Con: (Con:) FinalMultichar:* .#. ;

	_ FinalMultichar:* .#. ;




!NOTE: several attempts at vowel devoicing have been made, though all attempts
!have shown to lower coverage. 
!"devoicing of neutral vowels before voiceless fricatives, but not in second to last syllable."
![a:ȧ | e:ė | o:ȯ ] <=> _ (%>:) VoicelessFric: ;
!			except 
!			_ Eng:* %{N%}: ;
!			Vow: _ ;
!			h: _ ; 
!			_ VoicelessFric: Con:* (%>:) Vow: Con:* FinalMultichar:* .#. ; 
!			_ VoicelessFric: %>: Vow: FinalMultichar:* .#. ; !hátseškeh>o{ʼ}{o}
!			_ Con: FinalMultichar:* .#. ;
!			!Canʼt have three in a row. Middle stays as voiced.  
!			\h: Vow: VoicelessFric: VoicelessFric:* _ VoicelessFric: VoicelessFric:* (%>:) Vow: VoicelessFric:* ;
!
!"penultimate devoicing"
![a:ȧ | e:ė | o:ȯ ] <=> _ (>: ) [t: | s: | š: | k: | x:] Con:*(>:) [%{e%}: | e: | é: ] FinalMultichar:* .#. ;
!	except _ Eng:* %{N%}: ;












"if the final high tone vowel is neutralized and there is a single high tone followed by a neutral tone, that neutral tone will become high."

[a:á | e:é | o:ó ] <=> HighVow _ Con: [Con: | Vow: ]* HighVow: Con:* FinalMultichar:* .#. ;

			except 

			_ t: .#. ; 



!RULES FOR COMBINING MORPHEMES. 

"e -> 0 morpheme boundary voiceless vowel."

[e:0 | o:0 ] <=> _ %>: [ė: | ȯ: | é: | ó: ]  ;





"voicesless becomes high tone if an e %> comes before it in morphology"

[ė:é | ȯ:ó] <=> [e: | o: ]%>: _ ; 



"> is bad"

>:0 <=> _ ;



"Possessive obviatative %{ǒ%}"

%{ǒ%}:0 <=> _ %>: ó: ;



!HANDLER FOR ENGLISH NAMES

"N is bad (used to distinguish English names from bible."

%{N%}:0 <=> _ ;



"%{H%} only happens after rlwyaeiou"

%{H%}:h <=> [a: | e: | i: | o: | u: | w: | y: | r: | l: ] %{N%}: %>: _ ;



!HANDLER FOR ODD POSSESSIVES.

"Odd possessives: é"

%{é%}:é <=> _ ;

"Odd possessives: n"

n:0 <=> _ e: %>: %{v%}: %{é%}: ;

"Odd possessives: prefix vowel"

[a:0 | e:0 ] <=> n: _ %>: %{v%}: %{é%}: ;

"Odd possessives: %{v%}:"

%{v%}:v <=> h: e: %>: _ ;





!VIA 

"vowel change in verb positive and negetive."

%{Ó%}:0 <=> _ ; 



"Vowel change in verb positive and negetive: Vowel"

[e:ó | a:á | é:ó] <=> _ >: %{Ó%}: ;

"High tone he before me in negetive forms"

%{E%}:é <=> _ >: m: e: ;





!VII



"Vowel with line over multichar never gets changed."

[%{ē%}:e | %{ō%}:o ]<=> _ ; 













